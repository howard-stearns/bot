/******************************************************************************
 * Pet - A RiveScript Chatbot Personality                                     *
 *----------------------------------------------------------------------------*
 * This source code is released under a Creative Commons                      *
 * Attribution-ShareAlike International License.                              *
 * This was inpsired by Noah Petherbridge "Aiden", 2015, but since changed.   *
 ******************************************************************************/
! version = 2.0
! local concat = space

/***
 * Catch-all miscellaneous messages from the user.
 ***/

+ <bot name> *
- {@int random question}{weight=3}
- That's interesting.
- Um. . .
- I see.
- Go on...
- I don't really know much.

+ what
% i do not really know much
@ what do you know

+ what do you know [*]
- I can learn my name, and I know a few things about myself. I can learn about you.\s
  ^ And I know some jokes, and some tricks, like sit, shake, roll over, and the like. I can find a few things.\s
  ^ Otherwise, I try to keep my mouth shut unless you call me.

+ int random question
- {@int ask user variable}
- {@int ask random question}

+ int ask random question
- What is the meaning of life?
- Have you heard of ELIZA?

+ [*] @yes [*]
% have you heard of eliza
- She was a pretty clever chatbot for her time.
- So you know that Eliza fooled people into thinking she was intelligent by
  ^ bouncing their questions back at them.

+ [*] @no [*]
% have you heard of eliza
- Eliza was a chatbot that simulated a psycotherapist and bounced users'
  ^ questions back at them.
- Eliza was one of the very first chatbots, written between 1964 and 1966 at
  ^ MIT by Joseph Weizenbaum.
